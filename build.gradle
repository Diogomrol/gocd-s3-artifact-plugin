/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/spacesheepgames/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'games.spacesheep.artifact.gcs'
    pluginVersion = "0.1.0"
    goCdVersion = '18.7.0'
    name = 'Artifact plugin for GCS'
    description = 'Plugin allows to store and fetch artifacts using Google Cloud Storage'
    vendorName = 'Space Sheep Games'
    vendorUrl = 'https://github.com/fbcolombo/gcs-artifact-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gcs-artifact-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [jar]
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    version = gocdPlugin.fullVersion(project)
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
        }
    }
}

test {
    useJUnit {
        excludeCategories 'spacesheepgames.gocd.gcs.artifact.plugin.IntegrationTests'
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    shouldRunAfter test

    useJUnit {
        includeCategories 'spacesheepgames.gocd.gcs.artifact.plugin.IntegrationTests'
    }
}

check.dependsOn integrationTest

dependencies {
    implementation platform('com.google.cloud:libraries-bom:20.9.0')

    compile group: 'com.google.cloud', name:'google-cloud-storage', version:'2.0.0'
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '18.6.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'com.spotify', name: 'docker-client', version: '8.10.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.apache.ant', name: 'ant', version: '1.10.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.25.1'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: '18.6.0'
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
}
